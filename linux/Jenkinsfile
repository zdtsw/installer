/*
--------------------------------------------
buildArch   RPM         DEB         Comments
--------------------------------------------
x86_64      x86_64      amd64
armv7l      armv7hl     armhf       arm32
aarch64     aarch64     arm64       arm64
ppc64le     ppc64le     ppc64le
source      src         -           only for SRPM and no need specify as option or target
s390x       s390x       s390x       only for jdk8+
*/

env.NODE_LABEL_RPM = "dockerBuild&&linux&&x64" // RedHat + Suse + Debian x64
env.NODE_LABEL_DEB = "docker&&linux&&${ARCH}"  // Debian non-x64 
env.PRODUCT = "temurin"

pipeline {
    agent {
        label NODE_LABEL_RPM
    }
    options {
        timeout(time: 2, unit: 'HOURS') 
    }
    parameters {
        choice(name: 'VERSION', choices: ['8', '11', '17', '19'], description: 'Build for specific JDK VERSION')
        choice(name: 'ARCH', choices: ['x86_64', 'armv7l', 'aarch64', 'ppc64le', 's390x', 'all'], description: "Build for specific platform\n s300x not for VERSION 8\n")
        choice(name: 'DISTRO', choices: ['all', 'Debian', 'RedHat', 'Suse'], description: 'Build for specific Distro\n Select RPM builds for RedHat and Suse')
        booleanParam(name: 'uploadPackage', defaultValue: false, description: 'Tick this box to upload the deb/rpm files exlude src.rpm to Artifactory')
        booleanParam(name: 'uploadSRCRPM', defaultValue: false, description: 'Tick this box to upload the src.rpm files to Artifactory')
        string(name: 'gitrepo', defaultValue: 'https://github.com/adoptium/installer', description: 'Do not modify unless want to build from a specific gitrepo')
        string(name: 'gitbranch', defaultValue: 'master', description: 'Do not change unless want to build on a specific branch from above gitrepo')
        booleanParam(name: 'enableDebug', defaultValue: false, description: 'Tick to enable --stacktrace for gradle build')
    }
    tools {
        // this is set in the jenkins global tool
        jdk "jdk-11.0.13+8"
    }
    stages {
        stage ("Prepare Build"){
            steps{
                script{
                    currentBuild.displayName =  "jdk${VERSION} - ${ARCH} - ${DISTRO}"
                    currentBuild.description = env.BUILD_USER_ID
                    sh("docker --version")
                }
                checkout(
                    [
                        $class: 'GitSCM',
                        branches: [[name: "$gitbranch"]],
                        extensions: [[$class: 'CleanBeforeCheckout', deleteUntrackedNestedRepositories: true]],
                        userRemoteConfigs: [[url: "$gitrepo"]]
                    ]
                )
                dir("linux") {
                    stash name: "installercode", includes: "**"
                }
            }
        }
        stage ("BUILD")
        {
            parallel{
                stage('Build Installer for Debian') {
                    when  {
                        beforeAgent true
                        anyOf {
                            expression{ params.DISTRO == 'all' }  
                            expression{ params.DISTRO == 'Debian' } // only trigger debian build
                        }
                    }
                    agent {
                        label "${ARCH}" == "x86_64" ? "${env.NODE_LABEL_RPM}": "${env.NODE_LABEL_DEB}"
                    }
                    steps{
                        dir('linuxDebian') {
                            script {
                                DISTRO = "Debian"
                                echo "Installer Job for Temurin jdk ${VERSION} - ${ARCH} - ${DISTRO}"
                                setup("${DISTRO}", "${ARCH}")
                                unstash 'installercode'
                                buildAndTest("${DISTRO}", "${ARCH}")
                                if (params.uploadPackage.toBoolean()) {
                                    echo "Upload artifacts for ${VERSION} - ${ARCH} - ${DISTRO}"
                                    uploadArtifacts("${DISTRO}", "${ARCH}")
                                }
                            }
                        }
                    }
                }
                stage('Build Installer for Redhat') {
                    when  {
                        beforeAgent true
                        anyOf {
                            expression{ params.DISTRO == 'all' }
                            expression{ params.DISTRO == 'RedHat' }  // only trigger redhat build
                        }
                    }
                    steps{
                        dir('linuxRedHat') {
                            script {
                                DISTRO = "RedHat"
                                echo "Installer Job for Temurin jdk ${VERSION} - ${ARCH} - ${DISTRO}"
                                setup("${DISTRO}", "${ARCH}")
                                unstash 'installercode'
                                buildAndTest("${DISTRO}", "${ARCH}")
                                if (params.uploadPackage.toBoolean()) {
                                    echo "Upload artifacts for ${VERSION} - ${ARCH} - ${DISTRO}"
                                    uploadArtifacts("${DISTRO}", "${ARCH}")
                                }
                            }
                        }
                    }
                }
                stage('Build Installer for Suse') {
                    when  {
                        beforeAgent true // do condition when before allocate to agent
                        anyOf {
                            expression{ params.DISTRO == 'all' }
                            expression{ params.DISTRO == 'Suse' } // only trigger suse build
                        }
                    }
                    steps{
                        dir('linuxSuse') {
                            script {
                                DISTRO = "Suse"
                                echo "Installer Job for Temurin jdk ${VERSION} - ${ARCH} - ${DISTRO}"
                                setup("${DISTRO}", "${ARCH}")
                                unstash 'installercode'
                                buildAndTest("${DISTRO}", "${ARCH}")
                                if (params.uploadPackage.toBoolean()) {
                                    echo "Upload artifacts for ${VERSION} - ${ARCH} - ${DISTRO}"
                                    uploadArtifacts("${DISTRO}", "${ARCH}")
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

/*
* Common Functions
*/
def setup(DISTRO, buildArch) {
    cleanWs()
    // Docker --mount option requires BuildKit
    env.DOCKER_BUILDKIT = buildArch == "s390x" ? '0' : '1'
    env.COMPOSE_DOCKER_CLI_BUILD=1
    env._JAVA_OPTIONS =  (buildArch == "armv7l" && DISTRO == "Debian")? "" : "-Xmx4g"
}

def buildAndTest(DISTRO, buildArch) {
    try {
        if (DISTRO != "Debian") { // for RPM based: RedHat / Suse
            // Install Adoptium GPG key for RPM signing
            withCredentials([file(credentialsId: 'adoptium-artifactory-gpg-key', variable: 'GPG_KEY')]) {
                def buildCLI = "./gradlew packageJdk${DISTRO} checkJdk${DISTRO} --parallel -PPRODUCT=${env.PRODUCT} -PPRODUCT_VERSION=${VERSION} -PGPG_KEY=${GPG_KEY} -PARCH=${buildArch}"
                buildCLI = params.enableDebug.toBoolean() ? buildCLI + " --stacktrace" : buildCLI
                sh("$buildCLI")
            }
        } else {
            def gBuildTask = (buildArch == 'x86_64') ? "packageJdk${DISTRO} checkJdk${DISTRO}" : "packageJdk${DISTRO}"
            def debArchList = [
                "x86_64" : "amd64",
                "armv7l": "armhf",
                "aarch64": "arm64",
                "ppc64le": "ppc64el",
                "s390x"  : "s390x"
            ]
            def buildCLI = "./gradlew ${gBuildTask} --parallel -PPRODUCT=${env.PRODUCT} -PPRODUCT_VERSION=${VERSION} -PARCH=${debArchList[buildArch]}"
            buildCLI = params.enableDebug.toBoolean() ? buildCLI + " --stacktrace" : buildCLI
            sh("$buildCLI")
        }
    } catch (Exception ex) {
        echo 'Exception in buildAndTest: ' + ex.toString() 
        currentBuild.result = 'FAILURE'  // set the whole pipeline 'red' if build or test fail. Do not use "error" that will not call "finally"
    } finally {
        archiveArtifacts artifacts: '**/build/ospackage/*,**/build/reports/**,**/packageTest/dependencies/deb/*', onlyIfSuccessful:false, allowEmptyArchive: true
    }
}

def uploadArtifacts(DISTRO, buildArch) {
    if (DISTRO == "Debian") {
        uploadDebArtifacts(buildArch)
    } else {  // DISTRO == RPM(RedHat||Suse)
        uploadRPMArtifacts(DISTRO)
    }
}

def uploadDebArtifacts(buildArch) {
    // full list of all platforms, up to user to opt out s390x+jdk8
    def debArchList = [
        "x86_64" : "amd64",
        "armv7l": "armhf",
        "aarch64": "arm64",
        "ppc64le": "ppc64el",
        "s390x"  : "s390x"
    ]
    /*
        Debian/Ubuntu   10.0       11.0        16.04     20.04    22.04
        add more into list when avaiable for release
        also update linux/jdk/debian/main/packing/build.sh
    */
    def deb_versions = ["buster", "bullseye", "bionic", "focal", "jammy"]
    def distro_list = ""
    deb_versions.each { deb_version ->
        // Creates list like deb.distribution=stretch;deb.distribution=buster;
        distro_list += "deb.distribution=${deb_version};"
    }

    rtUpload ( //artifactory plugin
        serverId: 'adoptium.jfrog.io',
        failNoOp: true,
        spec: """{
            "files": [
                {
                "pattern": "**/build/ospackage/temurin-*${debArchList[buildArch]}.deb",
                "target": "deb/pool/main/t/temurin-${VERSION}/",
                "props": "${distro_list}deb.component=main;deb.architecture=${debArchList[buildArch]}"
                }
            ]
        }""",
    )
}

def uploadRPMArtifacts(DISTRO) {
    def distro_Package = [
        'redhat' : [
            'rpm/centos/7',
            'rpm/rocky/8',
            'rpm/rhel/7',
            'rpm/rhel/8',
            'rpm/rhel/9',
            'rpm/fedora/35',
            'rpm/fedora/36',
            'rpm/oraclelinux/7',
            'rpm/oraclelinux/8',
            'rpm/amazonlinux/2',

        ],
        'suse'   : [
            'rpm/opensuse/15.3',
            'rpm/opensuse/15.4',
            'rpm/sles/12',
            'rpm/sles/15'
        ]
    ]
    def packageDirs = distro_Package[DISTRO.toLowerCase()]
    // full list of all platforms, up to user to opt out s390x+jdk8 from input
    def rpmArchList = [
        "x86_64" : "x86_64",
        "armv7l": "armv7hl",
        "aarch64": "aarch64",
        "ppc64le": "ppc64le",
        "source" : "src",  // here we need src in the list to be able to upload
        "s390x"  : "s390x"
    ]
    // Enable upload src.rpm
    if ( params.uploadSRCRPM.toBoolean() || params.DISTRO == 'all'){   rpmArchList.put("Source","src") }

    packageDirs.each {
        packageDir ->
            rpmArchList.each {
                entry -> rtUpload (
                    serverId: 'adoptium.jfrog.io',
                    failNoOp: true,
                    spec: """{
                        "files": [
                            {
                            "pattern": "**/build/ospackage/*.${entry.value}.rpm",
                            "target": "${packageDir}/${entry.key}/Packages/"
                            }
                        ]
                    }"""
                )
            }
    }
}
